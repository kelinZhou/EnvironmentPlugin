apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: "com.kelin.environment"

environment {
    //当打生产包时将该参数改为true，那么所生成的EnvConfig类中就不会包含releaseEnv以外的内容，避免开发、测试等环境泄漏
    release false

    //配置初始化环境(应用首次安装到设备上的默认环境)，改参数也是用来配置manifestPlaceholders的环境的。
    initEnvironment "test"

    //当release的值为false时的包配置。
    devConfig {
        appIcon "@mipmap/ic_launcher"
        appName "环境插件"
//        versionCode 1
        versionName "1.0.0"
    }

    //当release的值为true时的包配置。
    releaseConfig {
        appIcon "@mipmap/ic_android"
        appName "@string/app_name"
//        versionCode 1
        versionName "2.0.0"
    }

    releaseEnv {
        alias "生产"

        variable "FILE_HOST", "https://file.lieluobo.com/"
        variable "GRPC_API_HOST", "api.lieluobo.com"
        variable "GRPC_API_PORT", "8443", true
        variable "OVERRIDE_AUTHORITY", "fett-api.lieluobo.com"
        variable "WEB_WEBSITE_PATH", "登录  s.lieluobo.com  并扫码", true
        variable "UM_APP_KEY", '5d2ec9f23fc195d3e000101c', true
        variable "WX_APP_ID", "wxc67742e80ede84bd"
        variable "WX_APP_SECRET", "9d98dbea78a98673e0ab1dcdcd99c82e"
        variable "J_PUSH_APP_KEY", "f75dcb203bf9ce802b0ecbb2"
        variable "TC_USER_ACTION_ID", "1109757651"
        variable "TC_APP_SECRET_KEY", "9150b5a1a133e03eb35cd3324094b7a4"
    }

    testEnv {
        alias "测试"

        variable "FILE_HOST", "http://file.testing.lieluobo.net/"
        variable "GRPC_API_HOST", "fett-api.lieluobo.testing"
        variable "OVERRIDE_AUTHORITY", ""
    }

    demoEnv {
        alias "预发"

        variable "FILE_HOST", "https://file.demo.lieluobo.com/"
        variable "GRPC_API_HOST", "api.demo.lieluobo.com"
        variable "GRPC_API_PORT", "4927"
        variable "OVERRIDE_AUTHORITY", ""
    }
}

android {

    compileSdkVersion 28
    defaultConfig {
        versionCode environment.versionCode
        versionName environment.versionName
        applicationId packageName
        minSdkVersion 14
        targetSdkVersion 28
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    android.applicationVariants.all {
        def flavors = it.productFlavors
        def channel = flavors.isEmpty() ? "" : flavors[0].name.split("-")[0] + "_"
        //获取当前时间的"YYYY-MM-dd"格式。assembleRelease
        def createTime = new Date().format("YYYY-MM-dd_HHmm", TimeZone.getTimeZone("GMT+08:00"))
        def buildType = it.buildType.name
        if (buildType == "release") {
            it.getPackageApplicationProvider().get().outputDirectory = new File(project.rootDir.absolutePath + "/apks/${it.applicationId}/${createTime}")
        }
        it.outputs.all {
            if (it.outputFile != null && 'release' == buildType) {
                if (it.outputFile.name.endsWith(".apk")) {
                    //重新对apk命名。
                    it.outputFileName = "${channel}EnvPlugin_v${versionName}_${createTime}-release.apk"
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    //支持多渠道配置。
    flavorDimensions "default"
    productFlavors {
        for (channel in file('./channels')) {
            if (channel != null && channel.length() > 0 && !channel.startsWith("//")) {
                "$channel-$packageName" {
                    //JPush的固定字段
                    manifestPlaceholders = ["JPUSH_PKGNAME": packageName, "JPUSH_CHANNEL": channel]
                    buildConfigField 'String', 'UM_CHANNEL', "\"$channel\""
                    dimension "default"
                }
                if (!environment.release) {
                    break
                }
            }
        }
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
