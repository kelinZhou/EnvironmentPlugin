apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: "com.kelin.environment"


environment {
    //当打生产包时将该参数改为true，那么所生成的EnvConfig类中就不会包含releaseEnv以外的内容，避免开发、测试等环境泄漏。
    online false

    //配置初始化环境(应用首次安装到设备上的默认环境)，改参数也是用来配置manifestPlaceholders的环境的。
    initEnvironment dev

    releaseConfig {
        appIcon "@mipmap/ic_android"
        appRoundIcon "@mipmap/ic_android"
        appName "EnvPlugin"
        versionName "1.0.0"
        versionCode 100
        applicationId "${packageName}.test"
        variable 'APK_BUILD_TYPE', "release"

        //resValue 'mipmap', 'APP_ICON', 'ic_android'
    }

    devConfig {
        appIcon "@mipmap/ic_launcher"
        appRoundIcon "@mipmap/ic_launcher"
        appName "@string/app_name"
        versionName "2.0.0"
        versionCode 200
        applicationId packageName
        variable 'APK_BUILD_TYPE', "test"
    }

    releaseEnv {
        alias "生产"

        variable "API_BASE_URL", "https://iyx.smart.api.intelliyx.com/"
        variable "API_VERSION", ""
        variable "BANK_SDK_APP_ID", "3031266f-54a8-46dd-ba47-2a0d516df29b"
        variable "BANK_SDK_APP_SECRET", "f4db09cf-80b2-4d3f-bda1-997ab41a3754"
        //定位API的路径
        variable "LOCATE_URL", "https://iyx.smart.api.intelliyx.com/api/v1/"
        variable "WE_CHAT_APP_ID", "wxf21bd8885522192d"
        variable "WE_CHAT_APP_SECRET", "2e8044fbb66f158fcc19e3ccec16fd06"
        variable "QQ_APP_ID", "1110499891", true
        variable "QQ_APP_KEY", "cmyrDyPBYXDudUtZ"
        variable "HR_SDK_ENV", "4", "int"  //4表示华瑞银行sdk为生产环境
    }

    devEnv {
        alias "bbb"

        variable "API_BASE_URL", "http://devTest.com/"
        variable "API_VERSION", "123456"
        variable "BANK_SDK_APP_ID", "123123123123123"
        variable "BANK_SDK_APP_SECRET", "xxxxxxxxxxxxx"
        //定位API的路径
        variable "LOCATE_URL", "http://asdfasdfasdfasdf"
        variable "HR_SDK_ENV", "6"  //1表示华瑞银行sdk为SIT环境

    }

    testEnv {
        alias "ccc"

        variable "API_BASE_URL", "http://sit.tms.api.yuchuanglian.com/"
        variable "API_VERSION", "180530"
        variable "BANK_SDK_APP_ID", "6cf89834-322d-466f-a5ba-df175f8f1a10"
        variable "BANK_SDK_APP_SECRET", "d18d04ab-f433-4242-86f3-c40facf71809"
        //定位API的路径
        variable "LOCATE_URL", "http://sit.tms.api.yuchuanglian.com:8020/api/v1/"
        variable "HR_SDK_ENV", "1"  //1表示华瑞银行sdk为SIT环境
    }

    demoEnv {
        alias "预发"

        variable "API_BASE_URL", "http://uat.tms.api.yuchuanglian.com/"
        variable "API_VERSION", "180530"
        variable "BANK_SDK_APP_ID", "6cf89834-322d-466f-a5ba-df175f8f1a10"
        variable "BANK_SDK_APP_SECRET", "d18d04ab-f433-4242-86f3-c40facf71809"
        //定位API的路径
        variable "LOCATE_URL", "http://uat.tms.api.yuchuanglian.com:8020/api/v1/"
        variable "HR_SDK_ENV", "2"  //2表示华瑞银行sdk为UAT环境
    }
}

android {

    compileSdkVersion 31
    defaultConfig {
        versionCode environment.versionCode
        versionName environment.versionName
        applicationId environment.applicationId
        versionCode 100
        minSdkVersion 14
        targetSdkVersion 31

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [APP_ICON: environment.appIcon,
                                APP_NAME:environment.appName,
                                APP_ROUND_ICON:environment.appRoundIcon,
        ]

        println("=========${environment.variables}")
        println("=========${environment.getVariable("API_BASE_URL")}")
    }

    signingConfigs {
        release {
            keyAlias 'kelinZhou'
            keyPassword '881219'
            storeFile file('../kelin.jks')
            storePassword '881219'
        }
    }

    buildTypes {
        release {
            //signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }


    //支持多渠道配置。
    flavorDimensions "default"
    productFlavors {
        for (channel in file('./channels')) {
            if (channel != null && channel.length() > 0 && !channel.startsWith("//")) {
                "$channel" {
                    //JPush的固定字段
                    manifestPlaceholders = ["JPUSH_PKGNAME": packageName, "JPUSH_CHANNEL": channel]
                    buildConfigField 'String', 'UM_CHANNEL', "\"$channel\""
                    dimension "default"
                }
                if (!environment.release) {
                    break
                }
            }
        }
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
